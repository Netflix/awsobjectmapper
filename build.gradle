
buildscript {
  repositories {
    mavenLocal()
    jcenter()
  }
}

plugins {
  id 'nebula.netflixoss' version '3.2.3'
}

// Establish version and status
ext.githubProjectName = 'awsobjectmapper'

allprojects {
  apply plugin: 'nebula.netflixoss'
  apply plugin: 'java'
  apply plugin: 'idea'
}

subprojects {

  repositories {
    mavenLocal()
    jcenter()
  }

  group = "com.netflix.${githubProjectName}"

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  javadoc {
    options {
      links = ['http://docs.oracle.com/javase/8/docs/api/']
    }
  }

  dependencies {
    compile 'com.amazonaws:aws-java-sdk:1.11.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.5.5'
    testCompile 'junit:junit:4.10'
    testCompile 'com.google.guava:guava:18.0'
    testCompile 'io.github.benas:jpopulator:1.0.1'
  }

  task copyDepsToBuild << {
    ['compile', 'runtime', 'testCompile', 'testRuntime'].each { conf ->
      delete "${buildDir}/dependencies/${conf}"
      copy {
        from configurations[conf]
        into "${buildDir}/dependencies/${conf}"
      }
    }
  }
}

project(':awsobjectmapper') {
  apply from: file('../gradle/awsMixin.gradle')

  compileJava.dependsOn generateAwsMixins

  sourceSets {
    main {
      java {
        srcDir 'build/generated'
      }
    }
  }
}
