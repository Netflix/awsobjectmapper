
plugins {
  id 'com.netflix.nebula.netflixoss' version '11.6.0'
}

// Establish version and status
ext.githubProjectName = 'awsobjectmapper'

allprojects {
  apply plugin: 'nebula.netflixoss'
  apply plugin: 'java-library'
  apply plugin: 'idea'
}

subprojects {

  repositories {
    mavenLocal()
    mavenCentral()
  }

  group = "com.netflix.${githubProjectName}"

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  javadoc {
    options {
      links = ['http://docs.oracle.com/javase/8/docs/api/']
    }
  }

  test {
    maxHeapSize = '4G'
  }

  ext {
    awsSdkVersion = '1.12.787'
  }

  dependencies {
    api "com.amazonaws:aws-java-sdk-autoscaling:${awsSdkVersion}"
    api "com.amazonaws:aws-java-sdk-cloudwatch:${awsSdkVersion}"
    api "com.amazonaws:aws-java-sdk-config:${awsSdkVersion}"
    api "com.amazonaws:aws-java-sdk-core:${awsSdkVersion}"
    api "com.amazonaws:aws-java-sdk-dynamodb:${awsSdkVersion}"
    api "com.amazonaws:aws-java-sdk-ec2:${awsSdkVersion}"
    api "com.amazonaws:aws-java-sdk-ecs:${awsSdkVersion}"
    api "com.amazonaws:aws-java-sdk-efs:${awsSdkVersion}"
    api "com.amazonaws:aws-java-sdk-elasticache:${awsSdkVersion}"
    api "com.amazonaws:aws-java-sdk-elasticloadbalancing:${awsSdkVersion}"
    api "com.amazonaws:aws-java-sdk-elasticloadbalancingv2:${awsSdkVersion}"
    api "com.amazonaws:aws-java-sdk-elasticsearch:${awsSdkVersion}"
    api "com.amazonaws:aws-java-sdk-iam:${awsSdkVersion}"
    api "com.amazonaws:aws-java-sdk-lambda:${awsSdkVersion}"
    api "com.amazonaws:aws-java-sdk-rds:${awsSdkVersion}"
    api "com.amazonaws:aws-java-sdk-route53:${awsSdkVersion}"
    api "com.amazonaws:aws-java-sdk-s3:${awsSdkVersion}"
    api "com.amazonaws:aws-java-sdk-ses:${awsSdkVersion}"
    api "com.amazonaws:aws-java-sdk-sns:${awsSdkVersion}"
    api "com.amazonaws:aws-java-sdk-sqs:${awsSdkVersion}"
    api "com.amazonaws:aws-java-sdk-ssm:${awsSdkVersion}"
    api "com.amazonaws:aws-java-sdk-sts:${awsSdkVersion}"
    api "com.amazonaws:aws-java-sdk-support:${awsSdkVersion}"
    api "com.amazonaws:aws-java-sdk-xray:${awsSdkVersion}"
    api 'com.fasterxml.jackson.core:jackson-databind:2.12.6'
    testImplementation 'junit:junit:4.10'
    testImplementation 'com.google.guava:guava:18.0'
    testImplementation 'org.jeasy:easy-random-core:4.0.0'
  }
}

project(':awsobjectmapper') {
  apply plugin: AwsMixinGenerator

  compileJava.dependsOn generateAwsMixins
  sourcesJar.dependsOn tasks.withType(JavaCompile), generateAwsMixins
  javadoc.dependsOn tasks.withType(JavaCompile), generateAwsMixins
  javadocJar.dependsOn tasks.withType(JavaCompile), generateAwsMixins
  licenseMain.dependsOn tasks.withType(JavaCompile), generateAwsMixins

  sourceSets {
    main {
      java {
        srcDir 'build/generated'
      }
    }
  }
}
